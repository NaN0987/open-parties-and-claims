plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT' apply(false)
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply(false)
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' apply(false)
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    //java.withSourcesJar()
    //java.withJavadocJar()

    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {

        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            url = "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
    }

    tasks.withType(JavaCompile).configureEach {

        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    def platform_server_core_class = 'xaero/pac/common/server/core/ServerCoreForge'
    def platform_payload_context_class = 'net/minecraftforge/event/network/CustomPayloadEvent$Context'
    def platform_fluid_stack_class = 'net/minecraftforge/fluids/FluidStack'

    if(project.name.equalsIgnoreCase("neoforge")){
        platform_server_core_class = 'xaero/pac/common/server/core/ServerCoreNeoForge'
        platform_payload_context_class = 'net/neoforged/neoforge/network/handling/PlayPayloadContext'
        //platform_fluid_stack_class = //NEEDED
    }

     processResources {
        def expandProps = [
                "version": version,
                "group": project.group, //Else we target the task's group.
                "minecraft_version": minecraft_version,
                "forge_version": forge_version,
                "forge_loader_version_range": forge_loader_version_range,
                "forge_version_range": forge_version_range,
                "minecraft_version_range": minecraft_version_range,
                "forge_minecraft_version_range": forge_minecraft_version_range,
                "fabric_version": fabric_version,
                "fabric_loader_version": fabric_loader_version,
                "fabric_loader_version_range": fabric_loader_version_range,
                "mod_name": mod_name,
                "mod_display_name": mod_display_name,
                "mod_author": mod_author,
                "mod_id": mod_id,
                "license": license,
                "description": project.description,
                "neoforge_version": neoforge_version,
                "neoforge_loader_version_range": neoforge_loader_version_range,
                "forgeconfigapiport_fabric_range": forgeconfigapiport_fabric_range,
                "forgeconfigapiport_forge_range": forgeconfigapiport_forge_range,
                "fabric_api_range": fabric_api_range,
                "create_forge_range": create_forge_range,
                "create_neoforge_range": create_neoforge_range,
                "fabric_extension_create_breaks_range": fabric_extension_create_breaks_range,
                "java_fabric_range": java_fabric_range,
                "homepage": homepage,
                "sources": sources,
                "issues": issues,

                "platform_server_core_class": platform_server_core_class,
                "platform_payload_context_class": platform_payload_context_class,
                "platform_fluid_stack_class": platform_fluid_stack_class
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json', 'META-INF/*_core.js']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    javadoc {
        options.encoding = 'UTF-8'
    }


    File assetsLangFolder = project(":Common").sourceSets.main.resources.srcDirs[0].toPath().resolve("assets/${mod_id}/lang").toFile()
    File dataLangFolder = new File("data/${mod_id}/lang")

    tasks.withType(Jar).configureEach {
        from(rootDir){
            include 'LICENSE'
        }
    }

    tasks.withType(ProcessResources).configureEach {
        from(assetsLangFolder) {
            into(dataLangFolder)//copying lang files from client resources to server data
        }
    }

    project.task('packageSources', type: Jar) {
        archiveClassifier = 'sources'
        from project(":Common").sourceSets.main.allSource
        from project.sourceSets.main.allSource
        from(assetsLangFolder) {
            into(dataLangFolder)//copying lang files from client resources to server data
        }
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {

        enabled = false
    }

    compileTestJava {
        enabled = false
    }
}